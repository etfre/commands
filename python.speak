on_load() => setState(language, python) print('python loaded')
is_active() => window.test('Visual Studio Code') && state.language == python
on_activate() => print('python language activated')
on_deactivate() => print('python language deactivated')

pyKeyword := continue | break | pass | true=True | false=False | none=None
pyKeywordAfter := yield | return | import | with | not
pyKeywordBetween := in | is | and | or
builtin_functions := print | length=len | any | all | open | zip | string=str | type | super | set | range | representation=repr | (is instance) = isinstance | id | min | max | input | float | integer=int

new function [<_dictation>] = 'def ():' {left}~3 snake_case($1)
new class [<_dictation>] = 'class :' {left} snake_case($1)
if statement = 'if :' {left}
else if statement = 'elif :' {left}
else statement = 'else :' {enter}
for statement = 'for i in :' {left}
main statement = "if __name__ == '__main__':" {enter} 'main()'
while statement = 'while :' {left}
read file = 'with open() as f:' {left}~7
write file = 'with open(, "w") as f:' {left}~12
keyword <pyKeyword> = $1
keyword <pyKeywordAfter> = $1 ' '
keyword <pyKeywordBetween> = ' ' $1 ' '
short key <pyKeywordBetween> = ' ' $1 ' '
function <builtin_functions> = $1 '()' {left}
slice = '[::]' 
new string = "''" {left}